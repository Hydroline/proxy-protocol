buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.minecraftforge.gradle.userdev.tasks.RenameJarInPlace
import org.gradle.api.file.DuplicatesStrategy

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

def loadProxyProtocolVersion(String forgeVersion, String fallbackVersion) {
    def sharedVersionScript = rootDir.parentFile ? new File(rootDir.parentFile, 'shared-version.gradle') : null
    if (sharedVersionScript?.exists()) {
        apply from: sharedVersionScript
        if (hasProperty('resolveProxyProtocolVersion')) {
            return resolveProxyProtocolVersion(forgeVersion)
        }
    }
    return fallbackVersion
}

group = 'com.hydroline.proxy.protocol'
version = loadProxyProtocolVersion('1.20.1', '2.0.0-forge-1.20.1')

archivesBaseName = 'proxy-protocol'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_17



minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'


            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'proxy_protocol'

            mods {
                proxy_protocol {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'proxy_protocol'

            mods {
                proxy_protocol {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'proxy_protocol'

            mods {
                proxy_protocol {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'proxy_protocol', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                proxy_protocol {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "proxy_protocol.refmap.json"

    config "proxy_protocol.mixins.json"
}

configurations {
    nettyCodecHaproxy
}

configurations.implementation.extendsFrom(configurations.nettyCodecHaproxy)

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // Most mods will only need this if they use Mixins or depend on mods that use Mixins
    maven { 
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.4.9'

    nettyCodecHaproxy('io.netty:netty-codec-haproxy:4.1.91.Final') {
        transitive = false
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

shadowJar {
    configurations = [project.configurations.nettyCodecHaproxy]
    archiveClassifier.set('shadow')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude('module-info.class')
    exclude('META-INF/versions/**/module-info.class')
}

def shadowJarTaskProvider = tasks.named('shadowJar', ShadowJar)

jar {
    manifest {
        attributes([
                "Specification-Title"     : "proxy_protocol",
                "Specification-Vendor"    : "PanSzelescik",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "PanSzelescik",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(shadowJarTaskProvider)
    from {
        zipTree(shadowJarTaskProvider.flatMap { it.archiveFile })
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(RenameJarInPlace).matching {
    it.name == 'reobfJar'
}.configureEach {
    dependsOn(shadowJarTaskProvider)
    input.set(shadowJarTaskProvider.flatMap { it.archiveFile })
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
